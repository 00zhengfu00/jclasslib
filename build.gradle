apply plugin: 'idea'

import org.apache.tools.ant.taskdefs.condition.Os

version = '5.1-beta3'
ext.mediaDir = file('media')
ext.libCompileDir = file('lib-compile')

buildDir = file('build/gradle')
ext.externalLibsDir = file("$buildDir/externalLibs")

buildscript {
    ext.kotlinVersion = '1.0.5'

    repositories {
        flatDir {
            dirs 'lib-compile'
        }
        maven {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url 'http://maven.ej-technologies.com/repository'
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // for local dokka in lib-compile
        //classpath ':dokka-fatjar'
        //classpath ':dokka-gradle-plugin'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.9'
        classpath "com.install4j:install4j-gradle:6.1.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {

    group = 'org.gjt.jclasslib'
    buildDir = new File(rootProject.buildDir, path.substring(1).replaceAll(':', '/'))
    version = rootProject.version

    repositories {
        flatDir {
            dirs libCompileDir, file('lib')
        }
        maven {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url 'http://maven.ej-technologies.com/repository'
        }
    }

    plugins.withType(JavaPlugin).whenPluginAdded {
        dependencies {
            testCompile 'org.testng:testng:6.8.8'
        }

        tasks.withType(JavaCompile).each {JavaCompile compileJava ->
            compileJava.with {
                String java6Home = System.getenv()['JAVA_1_6_HOME']
                if (java6Home) {
                    if (Os.isFamily(Os.FAMILY_MAC)) {
                        options.bootClasspath = "${java6Home}/../Classes/classes.jar"
                    } else {
                        options.bootClasspath = "${java6Home}/jre/lib/rt.jar"
                    }
                } else {
                    println 'Warning: Set JAVA_1_6_HOME to a Java 6 JDK'
                }

                sourceCompatibility = '1.6'
                targetCompatibility = '1.6'
            }
        }

        tasks.withType(Test).each {Test test ->
            test.with {
                useTestNG()
            }
        }
    }
}

task clean {
    doLast {
        delete externalLibsDir
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
    distributionType = Wrapper.DistributionType.ALL
}

task test()
task dist()

gradle.projectsEvaluated {
    getTasksByName('clean', true).each {task ->
        if (task != clean) {
            clean.dependsOn(task)
        }
    }
    getTasksByName('dist', true).each {task ->
        if (task != dist) {
            dist.dependsOn(task)
        }
    }
    getTasksByName('test', true).each {task ->
        if (task != test) {
            test.dependsOn(task)
        }
    }
}

idea {
    module {
        name = 'root'
        excludeDirs += files('build', 'dist', 'media')
    }
}