import com.install4j.gradle.Install4jTask

apply plugin: 'install4j'

install4j {
    if (project.hasProperty('install4jHomeDir')) {
        installDir = file(install4jHomeDir)
    }
}

task clean {
    doLast {
        delete mediaDir
    }
}

task media(type: Install4jTask) {

    dependsOn ':dist', ':clean'
    projectFile = file('resources/jclasslib.install4j')
    release = version as String
    disableSigning = !project.hasProperty("winCertPath") || !project.hasProperty("macCertPath")
    winKeystorePassword=project.findProperty("winKeystorePassword") ?: ''
    macKeystorePassword=project.findProperty("macKeystorePassword") ?: ''

    variables = [
            winCertPath: project.findProperty("winCertPath") ?: '',
            macCertPath: project.findProperty("macCertPath") ?: '',
    ]

    doFirst {
        if (!project.hasProperty('install4jHomeDir')) {
            throw new RuntimeException('Specify install4jHomeDir in gradle.properties and set it to an install4j installation directory')
        }
    }
}

